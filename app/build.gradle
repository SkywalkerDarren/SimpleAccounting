apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "io.github.skywalkerdarren.simpleaccounting"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.appRawVersion
        versionName rootProject.appSemVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    signingConfigs {
        releaseConfig {
            storeFile file("../keys.jks")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [currencylayer_token: project.hasProperty("CURRENCY_TOKEN") ?
                    CURRENCY_TOKEN : System.getenv("CURRENCY_TOKEN"),
                                    isDebug            : true]
            buildConfigField("boolean", "LOG", "true")
            applicationIdSuffix ".debug"
        }
        release {
            manifestPlaceholders = [currencylayer_token: project.hasProperty("CURRENCY_TOKEN") ?
                    CURRENCY_TOKEN : System.getenv("CURRENCY_TOKEN"),
                                    isDebug            : false]
            minifyEnabled false
            buildConfigField("boolean", "LOG", "false")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    compileOptions {
        targetCompatibility rootProject.javaVersion
        sourceCompatibility rootProject.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.javaVersion.toString()
    }
    buildToolsVersion rootProject.buildToolsVersion
    dexOptions {
        dexInProcess true
        preDexLibraries true
        javaMaxHeapSize "2g"//越大越好
    }
    dataBinding {
        enabled = true
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "androidx.legacy:legacy-support-v4:${legacyVersion}"
    implementation "androidx.legacy:legacy-support-v13:${legacyVersion}"
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "androidx.cardview:cardview:${cardviewVersion}"
    implementation "androidx.vectordrawable:vectordrawable:${vectordrawableVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${constraintlayoutVersion}"
    implementation "androidx.recyclerview:recyclerview:${recyclerviewVersion}"

    implementation "androidx.preference:preference-ktx:${preferenceVersion}"

    implementation "androidx.lifecycle:lifecycle-extensions:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycleVersion}"

    implementation "androidx.work:work-runtime-ktx:${workVersion}"
    implementation "androidx.work:work-runtime:${workVersion}"

    implementation "androidx.room:room-runtime:${roomVersion}"
    implementation "androidx.room:room-ktx:${roomVersion}"
    annotationProcessor "androidx.room:room-compiler:${roomVersion}"
    kapt "androidx.room:room-compiler:${roomVersion}"
    androidTestImplementation "androidx.room:room-testing:${roomVersion}"

    implementation "com.google.android.material:material:${materialVersion}"

    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    annotationProcessor "com.github.bumptech.glide:compiler:${glideVersion}"
    kapt "com.github.bumptech.glide:compiler:${glideVersion}"

    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"

    implementation "org.apache.commons:commons-csv:${csvVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "joda-time:joda-time:${jodaTimeVersion}"
    implementation "com.github.PhilJay:MPAndroidChart:${MPAndroidChartVersion}"
    implementation "com.github.ceryle:SegmentedButton:${segmentedButtonVersion}"
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:${brvahVersion}"
    implementation "com.oushangfeng:PinnedSectionItemDecoration:${pinnedSectionItemDecorationVersion}"

    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.ext:junit:${androidJunitVersion}"
    testImplementation "junit:junit:${junitVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
}
